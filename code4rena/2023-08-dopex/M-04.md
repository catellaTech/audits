# Low level calls to accounts with no code will succeed in UniV3LiquidityAmo:execute and RdpxDecayingBonds:emergencyWithdraw

## Lines of Code
- https://github.com/code-423n4/2023-08-dopex/blob/main/contracts/amo/UniV3LiquidityAmo.sol#L344
- https://github.com/code-423n4/2023-08-dopex/blob/main/contracts/decaying-bonds/RdpxDecayingBonds.sol#L98

## Vulnerability details
### Impact
Low level calls behave differently than function calls in Solidity. Calls at the EVM level `to accounts with no code are successful`, this is the expected and normal behavior. It is Solidity that adds checks to prevent accidental calls to accounts with no code while compiling code for normal function calls.

This means that if the target account has no code, then the `UniV3LiquidityAmo:execute` will be executed successfully and the same for `RdpxDecayingBonds:emergencyWithdraw` . An accidental mistake may go unnoticed and may also cause unexpected loss of funds, as this call may include call value for transferring `native currency`.

The `sponsor` has provided us with the following explanation regarding this `execute` function: 
```Shell
"We just added a generic proxy function in case of an unforeseeable situation we would use it."
``` 
and the function looks like:
- https://github.com/code-423n4/2023-08-dopex/blob/main/contracts/amo/UniV3LiquidityAmo.sol#L344

```solidity
  // Generic proxy
  function execute(
    address _to,
    uint256 _value,
    bytes calldata _data
  ) external onlyRole(DEFAULT_ADMIN_ROLE) returns (bool, bytes memory) {
    (bool success, bytes memory result) = _to.call{ value: _value }(_data); // @audit should check the address _to
    return (success, result);
  }
```

the sponsor let us know this about the `emergencyWithdraw` function:
```shell
0xcatellatech — hoy a las 10:42
Will the emergencyWithdraw from all contracts be sent to a contract or to an EOA?

psytama — hoy a las 11:37
maybe but once we have multisig it will be to the multisig so a contract.
```
- https://github.com/code-423n4/2023-08-dopex/blob/main/contracts/decaying-bonds/RdpxDecayingBonds.sol#L98
```solidity
  /**
   * @notice Transfers all funds to msg.sender
   * @dev    Can only be called by the owner
   * @param  tokens The list of erc20 tokens to withdraw
   * @param  transferNative Whether should transfer the native currency
   * @param  to The address to transfer the funds to
   * @param  amount The amount to transfer
   * @param  gas The gas to use for the transfer
   **/
  function emergencyWithdraw(
    address[] calldata tokens,
    bool transferNative,
    address payable to,
    uint256 amount,
    uint256 gas
  ) external onlyRole(DEFAULT_ADMIN_ROLE) {
    _whenPaused();
    if (transferNative) {
      (bool success, ) = to.call{ value: amount, gas: gas }(""); // @audit it should check the address to
      require(success, "RdpxReserve: transfer failed");
    }
    IERC20WithBurn token;

    for (uint256 i = 0; i < tokens.length; i++) {
      token = IERC20WithBurn(tokens[i]);
      token.safeTransfer(msg.sender, token.balanceOf(address(this)));
    }
  }

```

As written in the [solidity documentation](https://docs.soliditylang.org/en/develop/control-structures.html#error-handling-assert-require-revert-and-exceptions), the low-level functions call, delegatecall and staticcall return true as their first return value if the account called is non-existent, as part of the design of the EVM. Account existence must be checked prior to calling if needed.

### Proof of Concept
I make this PoC to illustrate better the issue.
Follow the following steps:

1. `copy and paste` this test in `tests/rdpxV2-core/Periphery.t.sol`

```solidity
    function testCallToAccountNoCodeSucceed() public {
        // we deploy the target contract to make the PoC
        uniV3LiquidityAMO = new UniV3LiquidityAMO(
        address(rdpx),
        address(rdpxV2Core)
        );

        address to = makeAddr("address with no code"); 
        uint256 value = 0; // No ether value to send
        bytes memory data = abi.encodeWithSignature("Success()"); 

        // `to` address has no code
        assertEq(to.code.length, 0);

        // Execute the function on the target contract
        (bool success,) = uniV3LiquidityAMO.execute(to, value, data);

        console.log("the status of the call is:", success);
        assertTrue(success);
        console.log("Call succeeds even though the target has no code and no implementation");
    }
```

2. Run the test with `forge test --match-test testCallToAccountNoCodeSucceed -vv`

the output will be:
```terminal
Running 1 test for tests/rdpxV2-core/Periphery.t.sol:Periphery
[PASS] testCallsToAccountsNoCodeSucceed() (gas: 2416426)
Logs:
  the status of the call is: true
  Call succeeds even though the target has no code and no implementation

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 1.20s
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```
### Tools Used
Foundry and Manual review.

### Recommended Mitigation Steps
While executing low level calls, the `UniV3LiquidityAmo:execute` and `RdpxDecayingBonds:emergencyWithdraw` function should check that either the address `has code or the return data is greater than zero` (which indicates the presence of an implementation). The OpenZeppelin contracts library provides utilities to execute low level calls in a safe way, including the `functionCall` and  `functionCallWithValue`.